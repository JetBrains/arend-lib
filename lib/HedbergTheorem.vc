\open ::Paths
\open ::Logic
\open ::Hlevel
\open ::Equiv
\open ::Combinators
\open ::Data::Empty
\open ::Relation
\open ::PropLogic

\function
axiomK (A : \Type0) => \Pi (x : A) (p : x = x) -> idp = p

\function
axK-impl-isSet (A : \Type0) (axK : axiomK A) : isSet A =>
    \lam x y p => Jl (\lam z (p' : x = z) => \Pi (q : x = z) ->  p' = q) (axK x) p

\function
refl-rel-lemma {A : \Type0} (R : A -> A -> \Prop) (r : ReflexiveP R) (f : \Pi (x y : A) -> R x y -> x = y) : isSet A =>
    axK-impl-isSet A (\lam x p =>
        \let | p' : (transport (\lam y => R x y -> x = y) p (f x x) = f x x) => pmapd (\lam y => R x y -> x = y) (\lam y => f x y) p
            | p'' (rp : R x x) : transport ((=) x) p (f x x rp) = f x x (transport (R x) p rp) =>
                transp-funcfib-paths (R x) ((=) x) p (f x x) (f x x) p' rp
            | d : (f x x (r x)) *> p = f x x (r x) =>
                (f x x (r x)) *> p                  ==< transp-pathfibers x p (f x x (r x)) >==
                transport ((=) x) p (f x x (r x))   ==< p'' (r x) >==
                f x x (transport (R x) p (r x))     ==< pmap (\lam q => f x x q) (prop-isProp (R x x) (transport (R x) p (r x)) (r x)) >==
                f x x (r x)                         !qed
        \in inv (p                                          ==< (inv (concat-idp p)) *> (pmap (\lam q => q *> p) (inv (inv-concat (f x x (r x))))) *> (concat-assoc (inv (f x x (r x))) (f x x (r x)) p) >==
                (inv (f x x (r x))) *> ((f x x (r x)) *> p) ==< pmap (\lam q => (inv (f x x (r x))) *> q) d >==
                (inv (f x x (r x))) *> (f x x (r x))        ==< inv-concat (f x x (r x)) >== idp
                                                            !qed))

\function
DNLawEq-impl-isSet (A : \Type0) (dn : \Pi (x y : A) ->  DNLaw (x = y)) : isSet A =>
    refl-rel-lemma (\lam x y => Not (Not (x = y))) (\lam x => double-neg idp) dn

\function
Hedberg-thm {A : \Type0} (dec-p : \Pi (x y : A) -> Dec (x = y)) : isSet A => DNLawEq-impl-isSet A (\lam x y => dec-impl-DNLaw (x = y) (dec-p x y))
