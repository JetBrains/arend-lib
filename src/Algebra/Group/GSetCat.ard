\import Algebra.Group
\import Algebra.Group.GSet
\import Category
\import Function (isInj, isSurj)
\import Function.Meta ($)
\import Paths
\import Set
\import Set.Category

\record EquivariantMaps (G : Group) \extends SetHom{
  \override Dom : GroupAction G
  \override Cod : GroupAction G
  | **-equiv {e : Dom} {g : G} : func (g ** e) = g ** func e

  \func isIso => \Sigma (isSurj func) (isInj func)
}

\func id-equivar {G : Group} (X : GroupAction G) : EquivariantMaps G { | Dom => X | Cod => X} \cowith
  | func (x : X) => x
  | **-equiv {x : X} {g : G} => idp

\instance GSet (G : Group) : Precat (GroupAction G)
  | Hom A B => EquivariantMaps G { | Dom => A | Cod => B}
  | id X => id-equivar X
  | o {x y z : GroupAction G} (g : EquivariantMaps G y z) (f : EquivariantMaps G x y) => \new EquivariantMaps G {
    | func (a : x) => g (f a)
    | **-equiv {a : x} {h : G} =>
      g ( f (h ** a)) ==< pmap g **-equiv >==
      g (h ** f a) ==< **-equiv >==
      h ** (g (f a)) `qed
  }
  | id-left => idp
  | id-right => idp
  | o-assoc => idp