\import Data.Unit
\import Data.Empty
\import Data.Either
\import Hlevel
\import Equiv
\import Paths
\import Combinators

\function
TrueP : \Prop => Unit

\function
trueP : Unit => unit

\function
False : \Prop => Empty

\function
~~~ (P : \Prop) => P -> False

\function
&&& (P Q : \Prop) : \Prop => \Sigma P Q

\function
||| (P Q : \Prop) : \Prop => TrP (Either P Q)

\function
<--> (P Q : \Prop) : \Prop => (P -> Q) &&& (Q -> P)

\function
exists {A : \Set} (P : A -> \Prop) : \Prop => TrP (\Sigma (x : A) (P x))

\function
isInh (A : \Set) : \Prop => TrP A

-- \function
-- (!) (A : \Type) : \Prop => TrP A

\function 
DecP (P : \Prop) => `~~~ P ||| P

\function
DNLawP (P : \Prop) => `~~~ (`~~~ P) -> P

\function
inP-inv (P : \Prop) (p : TrP P) : P => \elim p | inP p => p

\function
inP-isequiv (P : \Prop) : isequiv (inP {P})
  => qinv-to-equiv (inP {P}) (inP-inv P, \lam (p : P) => idp, \lam (p : TrP P) => path (truncP {P} (inP {P} (inP-inv P p)) p))

\function
TrP-isProp (A : \Type) : (isProp (TrP A)) => \lam a a' => path (truncP a a')

\function
prop-isProp (P : \Prop) : (isProp P) => ofHlevel1-isProp P (hlevel-equiv-closed (equiv-symm (inP {P}, inP-isequiv P)) (suc zero) (isProp-ofHlevel1 (TrP P) (TrP-isProp P)))

\function
prop-ext {P Q : \Prop} (eqv : P <--> Q) : `= {\Prop} P Q => path (iso (\levels \Prop) eqv.1 eqv.2 (\lam x => prop-isProp P (eqv.2 (eqv.1 x)) x) (\lam x => prop-isProp Q (eqv.1 (eqv.2 x)) x))

\function
prop-eq-to-equiv {P Q : \Prop} (p : P = Q) : (P <--> Q) => transport (\lam (X : \Prop) => P <--> X) p (id, id)

--(\lam p => prop-isProp P (g (f p)) p) (\lam q => prop-isProp Q (f (g q)) q))

\function
provable-is-true {P : \Prop} (p : P) : P = TrueP => prop-ext (\lam _ => trueP, \lam _ => p)

\function
fun-to-prop (X Y : \Type) (P : isProp Y) (x : X) : (X -> Y) = Y => 
    \let | G (y : Y) (x : X) => y
         | F (f : X -> Y) => f x
    \in equiv-to-path (F, ((G, \lam f => fun-ext (\lam (x : X) => P (G (F f) x) (f x))), (G, \lam y => idp)))
