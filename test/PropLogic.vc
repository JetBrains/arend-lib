\import Data.Unit
\import Data.Empty
\import Data.Either
\import Hlevel
\import Equiv
\import Paths
\import Combinators

\func TrueP : \Prop => Unit

\func trueP : Unit => unit

\func False : \Prop => Empty

\func ~~~ (P : \Prop) => P -> False

\func \infixr 9 &&& (P Q : \Prop) : \Prop => \Sigma P Q

\func \infixr 9 ||| (P Q : \Prop) : \Prop => TrP (Either P Q)

\func \infixr 9 <--> (P Q : \Prop) : \Prop => (P -> Q) &&& (Q -> P)

\func exists {A : \Set} (P : A -> \Prop) : \Prop => TrP (\Sigma (x : A) (P x))

\func isInh (A : \Set) : \Prop => TrP A

-- \func ! (A : \Type) : \Prop => TrP A

\func DecP (P : \Prop) => ~~~ P ||| P

\func DNLawP (P : \Prop) => ~~~ (~~~ P) -> P

\func inP-inv (P : \Prop) (p : TrP P) : P => \elim p | inP p => p

\func inP-isequiv (P : \Prop) : isequiv (inP {P})
  => qinv-to-equiv' (inP {P}) (inP-inv P, \lam (p : P) => idp, \lam (p : TrP P) =>   path (truncP {P} (inP {P} (inP-inv P p)) p))

\func TrP-isProp (A : \Type) : (isProp (TrP A)) => \lam a a' => path (truncP a a')

\func prop-isProp (P : \Prop) : (isProp P) => ofHlevel1-isProp P (hlevel-equiv-closed (equiv-symm (inP {P}, inP-isequiv P)) (suc zero) (isProp-ofHlevel1 (TrP P) (TrP-isProp P)))

\func prop-ext {P Q : \Prop} (eqv : P <--> Q) : P = {\Prop} Q => path (iso (\levels \Prop) eqv.1 eqv.2 (\lam x => prop-isProp P (eqv.2 (eqv.1 x)) x) (\lam x => prop-isProp Q (eqv.1 (eqv.2 x)) x))

\func prop-eq-to-equiv {P Q : \Prop} (p : P = Q) : (P <--> Q) => transport (\lam (X : \Prop) => P <--> X) p (id, id)

--(\lam p => prop-isProp P (g (f p)) p) (\lam q => prop-isProp Q (f (g q)) q))

\func provable-is-true {P : \Prop} (p : P) : P = TrueP => prop-ext (\lam _ => trueP, \lam _ => p)

\func fun-to-prop (X Y : \Type) (P : isProp Y) (x : X) : (X -> Y) = Y => 
    \let | G (y : Y) (x : X) => y
         | F (f : X -> Y) => f x
    \in equiv-to-path (F, ((G, \lam f => fun-ext (\lam (x : X) => P (G (F f) x) (f x))), (G, \lam y => idp)))

\func tfae3 (A B C : \Type) => \Sigma (pA : isProp A) (pB : isProp B) (pC : isProp C) (AB : A -> B) (BC : B -> C) (CA : C -> A)

\func tfae-eq12 {A B C : \Type} (E : tfae3 A B C) : A = B => prop-eq {A} {B} E.1 E.2 E.4 (E.6 `o E.5)
