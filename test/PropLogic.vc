\open ::Data::Unit
\open ::Data::Empty
\open ::Data::Either
\open ::Hlevel
\open ::Equiv
\open ::Paths
\open ::Combinators

\function
TrueP : \Prop => Unit

\function
trueP : Unit => unit

\function
False : \Prop => Empty

\function
~~~ (P : \Prop) => P -> False

\function
&&& (P Q : \Prop) : \Prop => \Sigma P Q

\function
||| (P Q : \Prop) : \Prop => TrP (Either P Q)

\function
<--> (P Q : \Prop) : \Prop => (P -> Q) &&& (Q -> P)

\function
exists {A : \Set} (P : A -> \Prop) : \Prop => TrP (\Sigma (x : A) (P x))

\function
isInh (A : \Set) : \Prop => TrP A

-- \function
-- (!) (A : \Type) : \Prop => TrP A

\function 
DecP (P : \Prop) => `~~~ P ||| P

\function
DNLawP (P : \Prop) => `~~~ (`~~~ P) -> P

\function
inP-inv (P : \Prop) (p : TrP P) : P => \elim p | inP p => p

\function
inP-isequiv (P : \Prop) : isequiv (TrP P).inP
  => (qinv-to-equiv (TrP P).inP (inP-inv P, \lam (p : P) => idp, \lam (p : TrP P) => path ((TrP P).truncP ((TrP P).inP (inP-inv P p)) p))).2

\function
TrP-isProp (A : \Type) : (isProp (TrP A)) => \lam a a' => path (truncP a a')

\function
prop-isProp (P : \Prop) : (isProp P) => ofHlevel1-isProp P (hlevel-equiv-closed (equiv-symm ((TrP P).inP, inP-isequiv P)) (suc zero) (isProp-ofHlevel1 (TrP P) (TrP-isProp P)))

\function
prop-ext {P Q : \Prop} (eqv : P <--> Q) : (P = Q) => path (iso eqv.1 eqv.2 (\lam x => prop-isProp P (eqv.2 (eqv.1 x)) x) (\lam x => prop-isProp Q (eqv.1 (eqv.2 x)) x))

\function
prop-eq-to-equiv {P Q : \Prop} (p : P = Q) : (P <--> Q) => transport (\lam (X : \Prop) => P <--> X) p (id, id)

--(\lam p => prop-isProp P (g (f p)) p) (\lam q => prop-isProp Q (f (g q)) q))

\function
provable-is-true {P : \Prop} (p : P) : (P = TrueP) => prop-ext (\lam x => trueP, \lam y => p)

\function
fun-to-prop (X Y : \Type) (P : isProp Y) (x : X) : (X -> Y) = Y => 
    \let | G (y : Y) (x : X) => y
         | F (f : X -> Y) => f x
    \in equiv-to-path (F, ((G, \lam f => fun-ext (\lam (x : X) => P (G (F f) x) (f x))), (G, \lam y => idp)))

\function
tfae3 (A B C : \Type) => \Sigma (pA : isProp A) (pB : isProp B) (pC : isProp C) (AB : A -> B) (BC : B -> C) (CA : C -> A)

\function
tfae-eq12 {A B C : \Type} (E : tfae3 A B C) : A = B => prop-eq {A} {B} E.1 E.2 E.4 (E.6 `o E.5)
